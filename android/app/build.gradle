def VERSION_CODE = 1
def VERSION_NAME = "0.0.1"
def PACKAGE_NAME = "jp.x28go.game.reversi"

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'deploygate'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    sourceSets {
        instrumentTest.setRoot('tests')
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion 14
        targetSdkVersion 25
        versionCode VERSION_CODE
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        develop {
            versionName VERSION_NAME + "-develop"
        }
        staging {
            versionName VERSION_NAME + "-staging"
        }
        product {
            versionName VERSION_NAME + "-product"
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name.equals("debug") || variant.buildType.name.equals("release")) {
            variant.outputs.all {
                outputFileName = "${appName}-android_v${versionName}_r${versionCode}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

deploygate {
    def keysProperties = project.file("../../keys.properties")
    if (keysProperties.exists()) {
        Properties properties = new Properties()
        keysProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def deploygateUser = properties.getProperty('deploygate.user')
        if (deploygateUser) {
            userName = deploygateUser
        } else {
            userName = System.getenv("DEPLOYGATE_USER")
        }
        def deploygateToken = properties.getProperty('deploygate.token')
        if (deploygateToken) {
            token = deploygateToken
        } else {
            token = System.getenv("DEPLOYGATE_TOKEN")
        }
    } else {
        userName = System.getenv("DEPLOYGATE_USER")
        token = System.getenv("DEPLOYGATE_TOKEN")
    }

    apks {
        debug {
            sourceFile = file("${project.buildDir}/outputs/apk/develop/debug/${appName}-android_v${VERSION_NAME}-develop-debug_r${VERSION_CODE}.apk")
        }
    }
}
